@top AlgorithmFile { AlgoritmoDeclaration+ }

@skip { space | LineComment }

@precedence {
  else @right,
  prefix,
  exp @left,
  times @left,
  plus @left,
  rel @left,
  and @left,
  or @left,
  assign @right,

  statement @cut
}

statement[@isGroup=Statement] {
  EscrevaStatement |
  LeiaStatement |
  AssignmentExpression |
  EscrevalStatement |
  LimpatelaStatement |
  SeStatement |
  EscolhaStatement |
  ParaSatatement
}

expression[@isGroup=Expression] {
  String |
  Number |
  Float |
  @specialize[@name=BooleanLiteral]<Identifier, "Verdadeiro" | "Falso"> |
  VariableName |
  ParenthesizedExpression |
  UnaryExpression |
  BinaryExpression
}

Type {
  kw<"Caractere"> |
  kw<"Inteiro"> |
  kw<"Real"> |
  kw<"Logico">
}

AlgoritmoDeclaration { 
  kw<"Algoritmo"> (Identifier | String) VariableDeclaration? BlockAlgoritmo
}

VariableDeclaration {
  kw<"Var"> (VariableDefinition)*
}

VariableDefinition {
  commaSep1<VariableName> ":" Type
}

AssignmentExpression {
  VariableName !assign "<-" expression
}

VariableName { Identifier }

commaSep<content> {
  (content ("," content)*)?
}

commaSep1<content> {
  content ("," content)*
}

UnaryExpression {
  !prefix (LogicOp<"NÃO"> | ArithOp<"+" | "-">) expression
}

BinaryExpression {
  expression !exp ArithOp<"^"> expression |
  expression !times (ArithOp<"/"> | ArithOp<"%"> | ArithOp<"*">) expression |
  expression !plus ArithOp<"+" | "-"> expression |
  expression !rel CompareOp expression |
  expression !and LogicOp<"E"> expression |
  expression !or LogicOp<"OU"> expression
}

ParenthesizedExpression { "(" expression ")" }

BlockAlgoritmo {
  kw<"Inicio"> (statement | expression)* kw<"Fim">
}

EscrevaStatement {
  kw<"Escreva"> expression
}

EscrevalStatement {
  kw<"Escreval"> expression
}

LimpatelaStatement {
  kw<"Limpatela"> expression
}

LeiaStatement {
  kw<"Leia"> VariableName
}

SeStatement {
  kw<"Se"> expression kw<"Então"> (statement | expression)* (SenaoStatement)? kw<"Fimse">
}

SenaoStatement {
  !else kw<"Senão"> (statement | expression)*
}

EscolhaStatement {
  kw<"Escolha"> expression (CasoStatement)* (OutrocasoStatement)? kw<"Fimescolha">
}

CasoStatement {
  kw<"Caso"> commaSep1<expression> CasoBlock
}

OutrocasoStatement {
  kw<"Outrocaso"> CasoBlock
}

CasoBlock {
  (statement | expression)*
}

ParaSatatement {
  kw<"Para"> VariableName kw<"de"> expression kw<"ate"> expression (kw<"passo"> expression)? kw<"Faça"> ParaBlock kw<"Fimpara">
}

ParaBlock {
  (statement | expression)*
}

LogicOp<expr> { @specialize[@name={expr}]<Identifier, expr> }

kw<term> { @specialize[@name={term}]<Identifier, term> }

@tokens {
  Identifier { identifierChar (identifierChar | @digit)* }
  identifierChar { @asciiLetter | $[_$\u{a1}-\u{10ffff}] }
  String {
    '"' (![\\\n"] | "\\" _)* '"'
  }
  LineComment { "//" ![\n]* }
  space { $[ \t\n\r]+ }
  Number { @digit+ }
  Float { @digit+ "." @digit+ }
  
  "(" ")"

  ArithOp<expr> { expr }
  CompareOp { "<" | ">" | ">=" | "<=" | "=" | "<>" }

  "<-"[@name=Equals]

  @precedence { LineComment, ArithOp<"/"> }
  @precedence { Float, Number }
}